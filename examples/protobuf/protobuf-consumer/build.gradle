plugins {
    id 'java-library'
    id 'com.google.protobuf' version '0.8.14'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation "io.grpc:grpc-protobuf:1.35.0"
    implementation "io.grpc:grpc-stub:1.35.0"
    implementation "io.grpc:grpc-netty:1.35.0"
    implementation "javax.annotation:javax.annotation-api:1.3.2"

    protobuf files('../../../proto/')

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'au.com.dius.pact.consumer:junit5:4.3.0-beta.2'
    testImplementation 'ch.qos.logback:logback-classic:1.2.5'
}

tasks.named('test') {
    useJUnitPlatform()
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:3.12.0" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:1.35.0" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}
