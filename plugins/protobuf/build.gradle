import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import java.nio.file.Paths

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.9.24'
    id 'application'
    id 'com.google.protobuf' version '0.9.4'
    id 'groovy'
}

version = '0.0.0'
targetCompatibility = '17'
sourceCompatibility = '17'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.9.24'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-jdk9:1.8.1'
    implementation 'io.grpc:grpc-protobuf:1.64.0'
    implementation 'io.grpc:grpc-stub:1.64.0'
    implementation 'io.grpc:grpc-netty:1.64.0'
    implementation "io.grpc:grpc-kotlin-stub:1.4.1"
//    implementation "javax.annotation:javax.annotation-api:1.3.2"
    implementation('io.github.oshai:kotlin-logging-jvm:5.0.1') {
        exclude group: 'org.jetbrains.kotlin'
    }
    implementation('com.michael-bull.kotlin-result:kotlin-result:1.1.12') {
        exclude group: 'org.jetbrains.kotlin'
    }
    implementation 'au.com.dius.pact.core:support:4.6.9'
    implementation 'au.com.dius.pact.core:model:4.6.9'
    implementation 'au.com.dius.pact.core:matchers:4.6.9'
    implementation 'com.google.protobuf:protobuf-java:4.26.1'
    implementation 'ch.qos.logback:logback-classic:1.5.6'
    implementation 'com.github.os72:protoc-jar:3.11.4'
    implementation "io.pact.plugin.driver:core:0.4.2"
    implementation "org.apache.commons:commons-lang3:3.14.0"
    implementation 'com.google.guava:guava:33.2.0-jre'
    implementation 'org.apache.tika:tika-core:2.9.2'

    protobuf files('../../proto/')

    testImplementation "org.codehaus.groovy:groovy:3.0.21"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.10.2'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation('org.spockframework:spock-core:2.3-groovy-4.0')
    testImplementation 'net.bytebuddy:byte-buddy:1.9.16'
    testImplementation 'org.objenesis:objenesis:3.4'
}

application {
    mainClass = 'io.pact.protobuf.plugin.PluginAppKt'
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "17"
    }
}

test {
    useJUnitPlatform()
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:4.26.1" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:1.64.0" }
        grpckt { artifact = "io.grpc:protoc-gen-grpc-kotlin:1.4.1:jdk17@jar" }
    }
    generateProtoTasks {
        all()*.plugins {
          grpc {}
          grpckt {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
        kotlin {
            srcDirs 'build/generated/source/proto/main/grpckt'
        }
    }
}

tasks.register('unpackDist', Copy) {
    dependsOn tasks.build
    from zipTree("${project.buildDir}/distributions/pact-protobuf-plugin-${project.version}.zip")
    eachFile { fcd ->
        fcd.relativePath = new RelativePath(true, fcd.relativePath.segments.drop(1))
    }
    into file(Paths.get(System.getProperty('user.home')).resolve(".pact/plugins/protobuf-${project.version}"))
}

tasks.register('copyManifest', Copy) {
    from 'pact-plugin.json'
    into file(Paths.get(System.getProperty('user.home')).resolve(".pact/plugins/protobuf-${project.version}"))
}

tasks.register('installLocal') {
    dependsOn 'unpackDist', 'copyManifest'
}
